CREATURE_ALIVE=LIST()
CREATURE_MAZE_COLUMN=LIST()
CREATURE_MAZE_ROW=LIST()
CREATURE_ROOM_COLUMN=LIST()
CREATURE_ROOM_ROW=LIST()
CREATURE_TYPE=LIST()
DEF PLACE_CREATURE(I)
    MX=CREATURE_MAZE_COLUMN(I)
    MY=CREATURE_MAZE_ROW(I)
    RM=GET_ROOM_MAP(MX,MY)
    TI=GET_CREATURETYPE_TILE_INDEX(CREATURE_TYPE(I))
    MSET RM,2,CREATURE_ROOM_COLUMN(I),CREATURE_ROOM_ROW(I),TI
ENDDEF
DEF REMOVE_CREATURE(I)
    MX=CREATURE_MAZE_COLUMN(I)
    MY=CREATURE_MAZE_ROW(I)
    RM=GET_ROOM_MAP(MX,MY)
    MSET RM,2,CREATURE_ROOM_COLUMN(I),CREATURE_ROOM_ROW(I),TILE_EMPTY
ENDDEF
DEF CREATE_CREATURE(CT,MX,MY,X,Y)
    'TODO: REUSE DEAD CREATURES WHEN POSSIBLE
    I=LEN(CREATURE_ALIVE)
    PUSH(CREATURE_ALIVE,TRUE)
    PUSH(CREATURE_TYPE,CT)
    PUSH(CREATURE_MAZE_COLUMN,MX)
    PUSH(CREATURE_MAZE_ROW,MY)
    PUSH(CREATURE_ROOM_COLUMN,X)
    PUSH(CREATURE_ROOM_ROW,Y)
    PLACE_CREATURE(I)
    RETURN I
ENDDEF
DEF GENERATE_CREATURE(CT)
    L=GET_CREATURETYPE_MINIMUM_EXIT_COUNT(CT)
    H=GET_CREATURETYPE_MAXIMUM_EXIT_COUNT(CT)
    DO
        MX=RND(0,MAZE_COLUMNS-1)
        MY=RND(0,MAZE_ROWS-1)
        E=GET_MAZE_CELL_EXITS(MX,MY)
        DO
            X=RND(0,ROOM_COLUMNS-1)
            Y=RND(0,ROOM_ROWS-1)
            RM=GET_ROOM_MAP(MX,MY)
            TI=MGET RM,1,X,Y
            CTI=MGET RM,2,X,Y
        UNTIL TI=TILE_FLOOR AND CTI=TILE_EMPTY
    UNTIL E>=L AND E<=H
    RETURN CREATE_CREATURE(CT,MX,MY,X,Y)
ENDDEF
DEF GENERATE_CREATURES()
    FOR CTI=0 TO LEN(ALL_CREATURETYPES)-1
        CT=ALL_CREATURETYPES(CTI)
        SC=GET_CREATURETYPE_SPAWN_COUNT(CT)
        WHILE SC>0
            GENERATE_CREATURE(CT)
            SC=SC-1
        WEND
    NEXT CTI
ENDDEF
DEF MOVE_CREATURE(I,D)
    IF CREATURE_ALIVE(I) THEN
        REMOVE_CREATURE(I)
        X=CREATURE_ROOM_COLUMN(I)
        Y=CREATURE_ROOM_ROW(I)
        NX=STEP_X(D,X,Y)
        NY=STEP_Y(D,X,Y)
        MX=CREATURE_MAZE_COLUMN(I)
        MY=CREATURE_MAZE_ROW(I)
        IF NX<0 OR NY<0 OR NX>=ROOM_COLUMNS OR NY>=ROOM_ROWS THEN
            CREATURE_MAZE_COLUMN(I) = STEP_X(D,MX,MY)
            CREATURE_MAZE_ROW(I) = STEP_Y(D,MX,MY)
            IF NX<0 THEN
                CREATURE_ROOM_COLUMN(I)=NX+ROOM_COLUMNS
            ELSEIF NX>=ROOM_COLUMNS THEN
                CREATURE_ROOM_COLUMN(I)=NX-ROOM_COLUMNS
            ELSE
                CREATURE_ROOM_COLUMN(I)=NX
            ENDIF
            IF NY<0 THEN
                CREATURE_ROOM_ROW(I)=NY+ROOM_ROWS
            ELSEIF NY>=ROOM_ROWS THEN
                CREATURE_ROOM_ROW(I)=NY-ROOM_ROWS
            ELSE
                CREATURE_ROOM_ROW(I)=NY
            ENDIF
        ELSE
            TL=GET_ROOM_TILE(MX,MY,NX,NY)
            IF CAN_WALK_ON_TILE(TL) THEN
                CREATURE_ROOM_COLUMN(I)=NX
                CREATURE_ROOM_ROW(I)=NY
            ENDIF
        ENDIF
        PLACE_CREATURE(I)
    ENDIF
ENDDEF
